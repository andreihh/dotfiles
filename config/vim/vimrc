" vimrc: configures Vim.
"
" Requirements:
" - Terminal capabilities: truecolors, Nerd Font, OSC52
" - Tools: `git`, `curl`, `fzf`, `fd`, `ripgrep`

" [[ XDG specification ]]

" Define XDG directories if unset.
if empty($XDG_DATA_HOME)
  let $XDG_DATA_HOME = $HOME . '/.local/share'
endif
if empty($XDG_STATE_HOME)
  let $XDG_STATE_HOME = $HOME . '/.local/state'
endif

" Configure state files and directories.
for dir in ['', 'backup', 'swap', 'view', 'undo', 'session']
  call mkdir($XDG_STATE_HOME . '/vim/' . dir, 'p')
endfor
let &viminfofile = $XDG_STATE_HOME . '/vim/viminfo'
let &backupdir = $XDG_STATE_HOME . '/vim/backup//'
let &directory = $XDG_STATE_HOME . '/vim/swap//'
let &viewdir = $XDG_STATE_HOME . '/vim/view//'
let &undodir = $XDG_STATE_HOME . '/vim/undo//'
let g:startify_session_dir = $XDG_STATE_HOME . '/vim/session'
let g:netrw_home = $XDG_STATE_HOME

" Configure plugin files and directories.
let s:plug_script = $MYVIMDIR . '/autoload/plug.vim'
let s:plug_dir = $XDG_DATA_HOME . '/vim/plugged'
let s:plug_url =
    \ 'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'

" [[ Plugins ]]

" Enable native plugins.
packadd! editorconfig  " File encodings, indentation, etc.
packadd! comment  " Commenting lines
packadd! matchit  " Better % matching

" Install the `vim-plug` plugin manager. Commands:
" - :PlugInstall
" - :PlugUpdate
" - :PlugClean
if empty(glob(s:plug_script))
  execute '!curl -fLo "' . s:plug_script . '" --create-dirs ' . s:plug_url
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" Enable plugins via `vim-plug`.
call plug#begin(s:plug_dir)
" Core plugins
Plug 'tpope/vim-sensible'  " Sensible settings
Plug 'tpope/vim-surround'  " Better surround motions
Plug 'mhinz/vim-startify'  " Starting dashboard and session management
Plug 'easymotion/vim-easymotion'  " Better navigation motions
Plug 'ojroques/vim-oscyank'  " OSC52 support
" `tmux` integration
Plug 'christoomey/vim-tmux-navigator'  " Vim / `tmux` window navigation
Plug 'RyanMillerC/better-vim-tmux-resizer'  " Vim / `tmux` window resizing
" Searching
Plug 'junegunn/fzf'  " Core `fzf` integration
Plug 'junegunn/fzf.vim'  " Better `fzf` searching
" UI
Plug 'catppuccin/vim', { 'as': 'catppuccin' }  " Color scheme
Plug 'machakann/vim-highlightedyank'  " Highlight yanked text
Plug 'ryanoasis/vim-devicons'  " Nerd Font icons
Plug 'itchyny/lightline.vim'  " Better status and tab lines
Plug 'wellle/context.vim'  " Show current context
" VCS
Plug 'mhinz/vim-signify'  " VCS gutter signs for changed lines
Plug 'tpope/vim-fugitive'  " Git integration
" Formatting
Plug 'google/vim-maktaba'  " Required dependency for `vim-codefmt`
Plug 'google/vim-glaive'  " Flag configuration for `vim-codefmt`
Plug 'google/vim-codefmt'  " Formatter registry and formatting commands
call plug#end()
call glaive#Install()

" [[ Options ]]

" Make Esc more responsive.
set ttimeoutlen=5

" Enable true colors. Disable if not supported by the terminal.
set termguicolors

" Use colors that look good on a dark background.
set background=dark

" Configure mode cursors: blinking bar in insert / replace and blinking bar
" elsewhere. Must be set after `termguicolors` or `set t_Co=256`.
let &t_SI = "\e[5 q"
let &t_SR = "\e[3 q"
let &t_EI = "\e[1 q"

" Always assume Unix-style files.
set fileformats=unix

" Don't show the mode, it's already in the status line.
set noshowmode

" Show command in the last line of the screen.
set showcmd

" Don't automatically wrap lines.
set nowrap

" Enable line numbers.
set number

" Enable relative line numbers.
set relativenumber

" Enable sign column.
set signcolumn=yes

" Sets how to display split and fold delimiters.
set fillchars=vert:│,fold:·,foldsep:│

" Sets how to display certain whitespace characters in the editor.
set list
set listchars=tab:»\ ,trail:·,nbsp:␣,precedes:◀,extends:▶

" Configure how new splits should be opened.
set splitbelow
set splitright

" Round indents to nearest multiple of `shiftwidth`.
set shiftround

" Keep current indent when starting a new line.
set autoindent

" Keep current indent for wrapped lines.
set breakindent

" Do smart autoindenting when starting a new line.
set smartindent

" Don't auto-close folds when opening a file.
set foldlevelstart=99

" Use indent folds by default.
set foldmethod=indent

" Case-insensitive searching unless `\C` or one or more capital letters in the
" search term.
set ignorecase
set smartcase

" Highlight search matches.
set hlsearch

" Save undo history.
set undofile

" Disable backups.
set nobackup
set nowritebackup

" Enable omni-completion.
set omnifunc=syntaxcomplete#Complete
set completeopt=menuone,noselect,popup,fuzzy

" What to store in a session. Don't save:
" - Unloaded and hidden buffers
" - Global options and variables
" - Session directory
set sessionoptions=
    \blank,curdir,folds,help,tabpages,winsize,winpos,terminal,localoptions

" [[ Autocommands ]]

augroup autosession
  autocmd!
  autocmd User StartifyReady call s:AutoSession()
augroup END

" Show cursor line only in focused window.
augroup config_cursor_line
  autocmd!
  autocmd VimEnter,WinEnter,FocusGained * setlocal cursorline
  autocmd WinLeave,FocusLost * setlocal nocursorline
augroup END

augroup config_theme_highlights
  autocmd!
  autocmd ColorScheme * call s:ConfigThemeHighlights()
augroup END

augroup config_netrw_keymaps
  autocmd!
  autocmd filetype netrw call s:ConfigNetrwKeymaps()
augroup END

" [[ Commands ]]

command! DiffUnsaved let filetype=&ft
    \ | tab split
    \ | let t:is_diff_tab=1
    \ | leftabove vnew | read ++edit # | execute 'normal! 1Gdd'
    \ | execute 'setlocal bt=nofile bh=wipe nobl noswf ro noma ft=' . filetype
    \ | windo :diffthis

command! -nargs=? DiffGit tab split | let t:is_diff_tab=1 | Gvdiffsplit! <args>
command! DiffGitFileHistory Git log --oneline % | nnoremap <buffer> q :close<CR>
command! DiffGitHistory Git log --oneline | nnoremap <buffer> q :close<CR>
command! DiffGitBlame Git blame | nnoremap <buffer> q :close<CR>

" [[ Plugin configs ]]

" Auto-save current session.
let g:startify_session_persistence = 1

" Do not change current working directory when opening files.
let g:startify_change_to_dir = 0

" Ensure VCS gutter signs are enabled after restoring the session.
let g:startify_session_savecmds = ['SignifyEnableAll']

" Returns the session file for the current working directory.
function! s:SessionFile()
  return substitute(getcwd(), '[\\/:]', '%%', 'g') . '.vim'
endfunction

" Auto-load session for current working directory if it exists.
function! s:AutoSession()
  let l:session_file = s:SessionFile()
  if filereadable(g:startify_session_dir . '/' . l:session_file)
    execute 'SLoad ' . l:session_file
  endif
endfunction

" Toggle maximization of the current window.
let s:zen_mode = 0
function! s:ToggleZenMode() abort
  if s:zen_mode
    let s:zen_mode = 0
    call feedkeys("\<C-w>=", 'n')
  else
    let s:zen_mode = 1
    call feedkeys("\<C-w>\|\<C-w>_", 'n')
  endif
endfunction

" Highlight yank for 300 ms.
let g:highlightedyank_highlight_duration = 300

" Disable default EasyMotion keymaps.
let g:EasyMotion_do_mapping = 0

" Write all buffers before navigating outside of Vim.
let g:tmux_navigator_save_on_switch = 1

" If Vim is the zoomed pane, wrap around Vim instead of unzooming.
let g:tmux_navigator_disable_when_zoomed = 1

" Resize windows in increments of 5.
let g:tmux_resizer_resize_count = 5
let g:tmux_resizer_vertical_resize_count = 5

let g:fzf_action = {
    \ 'ctrl-t': 'tab split',
    \ 'ctrl-s': 'split',
    \ 'ctrl-v': 'vsplit',
    \ }

" Disable `netrw` banner and use long listing style.
let g:netrw_banner = 0
let g:netrw_liststyle = 1

" Disable current context by default and don't add mappings.
let g:context_enabled = 0
let g:context_add_mappings = 0

" Returns the current session name and icon (`nf-fa-folder`).
function! g:SessionName() abort
  if !filereadable(v:this_session)
    return ''
  endif
  let l:session_path = substitute(v:this_session, '%%', '/', 'g')
  let l:session_name = fnamemodify(l:session_path, ':t:r')
  return !empty(l:session_name) ? ' ' . l:session_name : ''
endfunction

" Returns the current Git branch name and icon (`nf-fa-code_branch`).
function! g:BranchName() abort
  let l:branch = FugitiveHead()
  return !empty(l:branch) ? ' ' . l:branch : ''
endfunction

" Returns the current filetype name and icon.
function! g:FileType() abort
  let l:icon = WebDevIconsGetFileTypeSymbol() . ' '
  return empty(&filetype) ? 'no ft' : l:icon . &filetype
endfunction

" Lightline separator icons:
" - `nf-pl-left_hard_divider`
" - `nf-pl-right_hard_divider`
" - `nf-pl-left_soft_divider`
" - `nf-pl-right_soft_divider`
let g:lightline = {
    \ 'colorscheme': $VIM_THEME,
    \ 'active': {
    \   'left': [['mode', 'paste'], ['session', 'branch'], ['filename']],
    \   'right': [['lineinfo'], ['percentwin']],
    \ },
    \ 'inactive': {
    \   'left': [['filename']],
    \   'right': [['lineinfo'], ['percentwin'], ['ft']],
    \ },
    \ 'separator': { 'left': "", 'right': "" },
    \ 'subseparator': { 'left': "", 'right': "" },
    \ 'component_function': {
    \   'session': 'g:SessionName',
    \   'branch': 'g:BranchName',
    \   'ft': 'g:FileType',
    \ },
    \ }

" [[ Color scheme ]]

function! s:ConfigThemeHighlights() abort
  if g:colors_name == 'catppuccin_macchiato'
    " Override window separator to match status line.
    highlight! VertSplit guifg=#A5ADCB guibg=#1E2030
  endif
endfunction

" If the terminal supports colors, try to set the configured color scheme.
"
" Must be set after the `background` and `syntax`. Must be set after the
" `config_theme_highlights` `augroup` and the `s:ConfigThemeHighlights` function
" definitions.
if &t_Co >= 256
  colorscheme $VIM_THEME
endif

" [[ Keymaps ]]

" Set `<leader>` key.
let g:mapleader = "\<space>"
noremap <space> <nop>

" Set macro completion trigger key.
set wildcharm=<C-z>

" Required to map the Alt key.
for key in 'hjkl=VS'
  execute "set <M-" . key . ">=\e" . key
endfor

" General:
" - M = [m]anage plugins
" - S = [s]ave buffer
" - Q = [q]uit all
" - q = [q]uit special window / tab or record macro
" - <esc> = clear search highlights
" - u = [u]ndo
" - <C-r> = [r]edo
" - <C-c> = [c]opy last yank to system clipboard using OSC52
" - <C-b> = send to [b]ackground
" - s + s/u/r = [s]ort [s]imple / [u]nique / [r]everse
" - v + <C-V> = [v]isual block mode
" - K = show [k]eyword help
" - <leader>ts = [t]oggle [s]pelling
map Y y$
nnoremap M :PlugStatus<CR>
nnoremap S :w<CR>
nnoremap Q :quitall<CR>
nnoremap <expr> q index(['help', 'quickfix'], &buftype) >= 0
    \ ? ':quit<CR>'
    \ : (exists('t:is_diff_tab') ? ':tabclose<CR>' : 'q')
nnoremap <esc> <C-c>:nohlsearch<CR>
nnoremap <C-c> :OSCYankRegister<CR>
nnoremap <C-b> :stop<CR>
xnoremap ss :sort<CR>
xnoremap su :sort u<CR>
xnoremap sr :sort!<CR>
nnoremap <leader>ts :setlocal spell! spelllang=en_us<CR>

" Navigation:
" - gj / gk = [g]o to previous / next location
" - gf = [g]o to [f]ile under cursor / selected [f]ile
" - gx = [g]o to URI with e[x]ternal system handler
" - g + {1-9} = [g]o to tab
" - <C-u/d> = jump half page [u]p / [d]own
" - { / } = jump to previous / next blank line
" - f/F/t/T/;/, = find character in line motions
" - <C-f> = multi-window [f]ind
" - [q / ]q / [Q / ]Q = jump to previous / next / first / last [q]uickfix
" - [l / ]l / [L / ]L = jump to previous / next / first / last [l]ocation
nnoremap gj <C-o>
nnoremap gk <C-i>
for i in '123456789'
  execute 'nnoremap g' . i . ' ' . i . 'gt'
endfor
nmap <C-f> <plug>(easymotion-overwin-f)
xmap <C-f> <plug>(easymotion-s)
nnoremap [q :cprev<CR>
nnoremap ]q :cnext<CR>
nnoremap [Q :cfirst<CR>
nnoremap ]Q :clast<CR>
nnoremap [l :lprev<CR>
nnoremap ]l :lnext<CR>
nnoremap [L :lfirst<CR>
nnoremap ]L :llast<CR>

" Marks:
" - m + {a-zA-Z} = set [m]ark
" - dm + {a-zA-Z} = [d]elete [m]ark
" - dm + !/* = [d]elete buffer / all [m]arks
" - ' + {a-zA-Z} = go to mark line
" - ` + {a-zA-Z} = go to mark
for m in "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
  execute 'nnoremap dm' . m . ' :delmarks ' . m . '<CR>'
endfor
nnoremap dm! :delmarks!<CR>
nnoremap dm* :delmarks a-zA-Z<CR>

" Window:
" - <C-s/v/t/z/x> = perform window action
" - <M-x> = close tab
" - <C-h/j/k/l> = navigate panes across Vim and `tmux`
" - <M-h/j/k/l/=> = resize panes across Vim and `tmux`
nnoremap <C-s> :split<CR>
nnoremap <C-v> :vsplit<CR>
nnoremap <C-t> :tabedit %<CR>
nnoremap <expr> <C-z> <SID>ToggleZenMode()
nnoremap <C-x> :quit<CR>
nnoremap <M-x> :tabclose<CR>
nnoremap <M-=> <C-w>=

" Terminal:
" - X = open terminal
" - <C-e> = [e]xit terminal mode
nnoremap X :terminal<CR>
tnoremap <C-e> <C-\><C-n>

" Session:
" - dS = [d]elete [s]ession
" - <M-s> = [s]ave session
nnoremap <expr> dS ':SDelete! ' . fnamemodify(v:this_session, ':t') . '<CR>'
nnoremap <expr> <M-s> ':SSave! ' . <SID>SessionFile() . '<CR>'

" Search:
" - s + h/'/b/f/F/g/c/j/l/q/:// = [s]earch
" - <C-j/k/f> = select next / previous / find item
" - <C-u/d> = scroll preview [u]p / [d]own
" - <C-l/p> = toggle [l]ist / [p]review wrapping for long lines
" - <tab> = accept selected / toggled items
" - <S-tab> = toggle selected item
" - <C-o/s/v/t> = open selected item in window
" - <C-c> / <esc> = [c]ancel
nnoremap s <nop>
nnoremap sh :Helptags<CR>
nnoremap s' :Marks<CR>
nnoremap sb :Buffers<CR>
nnoremap sf :Files<CR>
nnoremap sF :Files %:h<CR>
nnoremap sg :Rg<CR>
nnoremap sc :GFiles?<CR>
nnoremap sj :Jumps<CR>
nnoremap sl :lopen<CR>
nnoremap sq :copen<CR>
nnoremap s: :History:<CR>
nnoremap s/ :BLines<CR>

" Explorer:
" - g- = [g]o to [-] parent directory
" - gf = [g]o to [f]ile / [f]older
" - gx = [g]o to URI with e[x]ternal system handler
" - gh = [g]o to toggle [h]idden files
" - gs = [g]o to change [s]orting options
" - gr = [g]o to [r]efresh current directory
" - ad = [a]dd new [d]irectory
" - af = [a]dd new [f]ile
" - cc = [r]ename node
" - dd = [d]elete node
" - q = [q]uit
" - g? = show help
nnoremap g- :Explore<CR>
function! s:ConfigNetrwKeymaps() abort
  map <buffer> g- -
  map <buffer> gf <CR>
  map <buffer> gx x
  " Should map to just `s`, but need to append a no-op key to stop waiting.
  map <buffer> gs s<tab>
  map <buffer> gr <C-l>
  map <buffer> af %
  map <buffer> ad d
  map <buffer> cc R
  map <buffer> dd D
  map <buffer> q :Rexplore<CR>
  map <buffer> g? <F1>
  " Search in current directory rather than parent.
  nnoremap <buffer> s.f :Files %<CR>
endfunction

" VCS:
" - [c / ]c / [C / ]C = jump to previous / next / first / last [c]hanged hunk
" - dvu = [d]iff [v]iew [u]nsaved buffer changes
" - dv + o/O/h/H/b = [d]iff [v]iew [o]pen / (file) [h]istory / [b]lame
"   - q = [q]uit diff tab / window
"   - g? = show help
nnoremap dvu :DiffUnsaved<CR>
nnoremap dvo :DiffGit<CR>
nnoremap dvO :DiffGit<space>
nnoremap dvh :DiffGitFileHistory<CR>
nnoremap dvH :DiffGitHistory<CR>
nnoremap dvb :DiffGitBlame<CR>

" Completion:
" - <C-j/k> = trigger completion / select next / previous item
" - <C-z> (wildcharm) = trigger command completion
" - <tab> = accept selected item
" - <C-c> = [c]ancel
inoremap <expr> <C-j> pumvisible() ? '<C-n>' : '<C-x><C-o>'
inoremap <expr> <C-k> pumvisible() ? '<C-p>' : '<C-x><C-o>'
inoremap <expr> <tab> pumvisible() ? '<C-y>' : '<tab>'
inoremap <C-c> <C-e>
cnoremap <C-j> <C-n>
cnoremap <C-k> <C-p>
cnoremap <tab> <C-y>
cnoremap <C-c> <C-e>

" Code:
" - [m / ]m / [M / ]M = jump to previous / next start / end of [m]ethod
" - <leader>cc = toggle line comment
" - <leader>c = toggle comment
" - <leader>f = toggle [f]old
" - <leader>- = toggle navigation context
" - <leader>= = format
" - H = show [h]elp
nmap <leader>cc gcc
nmap <leader>c gc
xmap <leader>c gc
nnoremap <leader>f za
nnoremap <leader>- :echo expand('%')<CR>:ContextToggleWindow<CR>
nnoremap <leader>= :FormatCode<CR>
xnoremap <leader>= :FormatLines<CR>
nnoremap H K

" [[ Config overrides ]]

" Load optional user overrides.
runtime overrides.vim
