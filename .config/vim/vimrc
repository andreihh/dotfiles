" vimrc
"
" Requires Vim9+ and `ripgrep`.

" Set XDG-compliant directories.
let vim_home = $XDG_CONFIG_HOME . '/vim'
let vim_state = $XDG_STATE_HOME . '/vim'
let fzf_data = $XDG_DATA_HOME . '/fzf'

" Configure state files and directories.
call mkdir(vim_state, 'p')
let &viminfofile = vim_state . '/viminfo'
let &backupdir = vim_state . '//'
let &directory = vim_state . '//'
let &viewdir = vim_state . '//'
let &undodir = vim_state . '//'

" Remove if using Vim9.1.0327 or newer.
set runtimepath^=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after

" Install the `vim-plug` plugin manager. Commands:
" - :PlugInstall
" - :PlugUpdate
" - :PlugClean
if empty(glob(vim_home . '/autoload/plug.vim'))
  silent execute '!curl -fLo "' . vim_home . '/autoload/plug.vim" --create-dirs'
      \ . ' https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" Enable plugins via vim-plug.
call plug#begin(vim_home . '/plugged')
Plug 'editorconfig/editorconfig-vim'  " File encodings, indentation, etc.
Plug 'tpope/vim-sensible'  " Sensible settings.
Plug 'tpope/vim-surround'  " Better surround motions.
Plug 'tpope/vim-commentary'  " Commenting lines.
Plug 'mhinz/vim-signify'  " VCS gutter signs for changed lines.
Plug 'airblade/vim-rooter'  " Auto-cd to project root if any.
Plug 'mkitt/tabline.vim'  " Tab numbers in tab line.
Plug 'easymotion/vim-easymotion'  " Better navigation motions.
Plug 'udalov/kotlin-vim'  " Kotlin syntax highlighting.
" Better `tmux` integration.
Plug 'christoomey/vim-tmux-navigator'
Plug 'RyanMillerC/better-vim-tmux-resizer'
" Fuzzy searching.
Plug 'junegunn/fzf.vim'
Plug 'junegunn/fzf', { 'dir': fzf_data, 'do': './install --xdg --all' }
" Vim LSP plugins.
Plug 'prabirshrestha/vim-lsp'
Plug 'prabirshrestha/asyncomplete.vim'
Plug 'prabirshrestha/asyncomplete-lsp.vim'
Plug 'mattn/vim-lsp-settings'
" Code formatting.
Plug 'google/vim-maktaba'
Plug 'google/vim-glaive'
Plug 'google/vim-codefmt'
call plug#end()
call glaive#Install()

" Load common settings and key mappings.
runtime settings.vim

" Show cursor line only in focused pane.
augroup CursorLine
  au!
  au VimEnter * setlocal cursorline
  au WinEnter * setlocal cursorline
  au BufEnter * setlocal cursorline
  au WinLeave * setlocal nocursorline
augroup END

" Fuzzy searching:
" - ss (search everywhere in project)
" - sf (search files)
" - sc (search uncommitted Git files)
" - sb (search current buffer)
" - sh (search home directory)
" - Ctrl-n/p (cycle options)
" - Ctrl-o (open option in current buffer)
" - Ctrl-t (open option in new tab)
" - Ctrl-s/v (open option in horizontal / vertical split)
" - Ctrl-e (cancel)
nnoremap s <nop>
nnoremap ss :Rg<CR>
nnoremap sf :Files<CR>
nnoremap sc :GFiles?<CR>
nnoremap sb :BLines<CR>
nnoremap sh :FZF ~<CR>
let g:fzf_action = {
    \ 'ctrl-t': 'tab split',
    \ 'ctrl-s': 'split',
    \ 'ctrl-v': 'vsplit' }

" Code navigation:
" - gd (go to definition)
" - gD (go to declaration)
" - gi (go to implementation)
" - [r / ]r (go to previous / next reference)
" - [e / ]e (go to previous / next error)
" - [w / ]w (go to previous / next warning)
" - [d / ]d (go to previous / next diagnostic)
" - <leader><leader> + motion (trigger EasyMotion)
nmap gd <plug>(lsp-definition)
nmap gD <plug>(lsp-declaration)
nmap gi <plug>(lsp-implementation)
nmap [r <plug>(lsp-previous-reference)
nmap ]r <plug>(lsp-next-reference)
nmap [e <plug>(lsp-previous-error)
nmap ]e <plug>(lsp-next-error
nmap [w <plug>(lsp-previous-warning)
nmap ]w <plug>(lsp-next-warning)
nmap [d <plug>(lsp-previous-diagnostic)
nmap ]d <plug>(lsp-next-diagnostic)

" Show diagnostics inline and as float on cursor hover.
let g:lsp_diagnostics_virtual_text_align = 'after'
let g:lsp_diagnostics_virtual_text_padding_left = 2
let g:lsp_diagnostics_virtual_text_wrap = 'truncate'
let g:lsp_diagnostics_float_cursor = 1

" Code inspection and transformation:
" - <leader>h (show help)
" - <leader>f (find references)
" - <leader>t (type hierarchy)
" - <leader>r (rename)
" - <leader>c (comment line or selected range)
" - <leader>= (format file or selected range)
" - <leader>q (toggle quickfix pane)
" - Ctrl-f/b (scroll float forward / backward)
" - Ctrl-c (close float)
nmap <leader>h <plug>(lsp-hover-float)
nmap <leader>f <plug>(lsp-references)
nmap <leader>t <plug>(lsp-type-hierarchy)
nmap <leader>r <plug>(lsp-rename)
nnoremap <leader>= :FormatCode<CR>
vnoremap <leader>= :FormatLines<CR>
nnoremap <expr> <leader>q
    \ empty(filter(getwininfo(), 'v:val.quickfix'))
    \ ? ":copen\<CR>" : ":cclose\<CR>"
nmap <expr> <C-f> lsp#scroll(-5)
nmap <expr> <C-b> lsp#scroll(+5)

" Code actions shortcuts:
" - <leader>a (trigger code actions)
" - # (accept numbered option)
" - Ctrl-n/p (cycle options)
" - Enter (accept selected option)
" - Esc / Ctrl-c (cancel)
nmap <leader>a <plug>(lsp-code-action-float)

function! s:on_lsp_buffer_enabled() abort
  " Enable LSP autocompletion.
  setlocal omnifunc=lsp#complete
  if exists('+tagfunc') | setlocal tagfunc=lsp#tagfunc | endif

  " Override autocomplete action.
  imap <buffer> <C-space> <plug>(asyncomplete_force_refresh)
endfunction

augroup lsp_install
  au!
  " Call s:on_lsp_buffer_enabled only for languages that have the server
  " registered.
  autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END

Glaive codefmt shfmt_options=`['-i', '2', '-sr', '-ci', '-bn', '-kp']`
